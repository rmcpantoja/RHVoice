# Copyright (C) 2024  Mateo Cedillo <angelitomateocedillo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define ProcNumChar [ %-:mínus |
%€: euro |
%+: yapasqa |
%.: kuska |
%,: ch’iku ] ;

define NZDigit 1|2|3|4|5|6|7|8|9;
define Digit %0|NZDigit;
define UnformattedNumber [ Digit Digit^<8];
define Number  UnformattedNumber;

define Insertzeros 
[%0] -> illak || [ .#. | [.#. %0+]] _ ;

define InsertThousands 
[..] -> waranka || Digit _ Digit^3 .#. ; 

define InsertMillions 
[..] -> hunu || Digit _ Digit^3 waranka ;

define RemoveSkippedParts 
%0 %0 %0 [waranka|hunu] -> 0 ;

define Units %0:0|1:shuk|2:ishkay|3:kimsa|4:chusku|5:pichka|6:sukta|7:kanchis|8:pusak|9:iskun;

define Teens 1:0 [%0:chunka|1:chunkashuk|2:chunkaishkay|3:chunkakimsa|4:chunkachusku|5:chunkapichka|6:chunkasukta|7:chunkakanchis|8:chunkapusak|9:chunkaiskun];

define Tens %0:0|2:ishkaychunka|3:kimsachunka|4:chuskuchunka|5:pichkachunka|6:suktachunka|7:kanchischunka|8:pusakchunka|9:iskunchunka;

define Hundreds %0:0|1:patsak|2:ishkaypatsak|3:kimsapatsak|4:chuskupatsak|5:pichkapatsak|6:suktapatsak|7:kanchispatsak|8:pusakpatsak|9:iskunpatsak;

define UpToThousand %0:illak .P. [[(Hundreds) Teens] | [((Hundreds) Tens) Units]];

define DigitsToWords 
UpToThousand @->;

define ProcNumber Number .o. 
Insertzeros .o. 
InsertThousands .o. 
InsertMillions .o. 
RemoveSkippedParts .o. 
DigitsToWords ; 

regex ProcNumChar | ProcNumber;
